#!/bin/csh -f
#TEST-PROGRAM pmemd.cuda
#TEST-DESCRIP TO_BE_DEtermined
#TEST-PURPOSE regression, basic
#TEST-STATE   undocumented

#$1 = PREC_MODEL
#$2 = NETCDF

if( ! $?DO_PARALLEL ) then
  setenv DO_PARALLEL " "
  if( $?TESTsander ) then
      set sander = $TESTsander
  else
      set sander = ${AMBERHOME}/bin/pmemd.cuda_$1
  endif
else
  if( $?TESTsander ) then
      set sander = $TESTsander
  else
      set sander = ${AMBERHOME}/bin/pmemd.cuda_$1.MPI
  endif
endif

cat > mdin <<EOF
 short md, nve ensemble, trunc oct
 &cntrl
   ntx=1, irest=0,
   ntc=2, ntf=2, tol=0.0000001, 
   nstlim=20, ntt=0,
   tempi=300.0, temp0=300.0,
   ntb=1,ntp=0,
   ntpr=1, ntwr=10000, ntwx=5,
   dt=0.001, ig=71277, ioutfm=0,
 /
 &ewald
  nfft1=64,nfft2=64,nfft3=64, netfrc=0,
 /

EOF

set output = mdout.pure_wat_oct_nve
set mdcrd = mdcrd.pure_wat_oct_nve

set output_save = $output.GPU_$1
set mdcrd_save = $mdcrd.GPU_$1

touch dummy
$DO_PARALLEL $sander -O -o $output -r restrt -x $mdcrd < dummy || goto error
#Use different dacdif tolerances based on precision model.
if ( "$1" == "DPFP" ) then
  #7 sig figs
  ../../dacdif -r 1.0e-7 $output_save $output
else
  #5 sig figs
  ../../dacdif -r 1.0e-5 $output_save $output
endif

../../dacdif -t 2 $mdcrd_save $mdcrd

/bin/rm -f mdin logfile mdinfo dummy restrt
exit(0)

error:
echo "  ${0}:  Program error"
exit(1)









